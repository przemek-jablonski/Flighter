apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
  compileSdkVersion 27
  defaultConfig {
    applicationId "com.android.szparag.flighter"
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  flavorDimensions "standard"
  sourceSets { main.java.srcDirs += 'src/main/kotlin' }

  configurations.all { config ->
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
      config.resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
          details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
        }
      }
    }
  }
}

kapt {
  generateStubs = true
}

dependencies {

  //local
  implementation project(':mvi')
  implementation project(':my-extensions-base')
  implementation project(':my-extensions-android')
  implementation project(':my-extensions-rx')
  implementation project(':my-permissions')

  //testing
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  //kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  //support libs
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.0'
  implementation 'com.android.support:support-v4:27.1.1'
  implementation 'com.android.support:recyclerview-v7:27.1.1'
  implementation 'com.android.support:design:27.1.1'

  //google play services
  implementation 'com.google.android.gms:play-services-maps:15.0.1'
  implementation 'com.google.android.gms:play-services-location:15.0.1'

  //firebase
  implementation 'com.google.firebase:firebase-core:16.0.0'
  implementation 'com.google.firebase:firebase-database:16.0.1'

  //retrofit
  implementation 'com.squareup.retrofit2:retrofit:2.4.0'
  implementation 'com.google.code.gson:gson:2.8.4'
  implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava:2.4.0'

  //dagger
  implementation 'com.google.dagger:dagger:2.12'
  kapt 'com.google.dagger:dagger-compiler:2.12'
  kapt 'com.google.dagger:dagger-android-processor:2.12'
  compileOnly 'org.glassfish:javax.annotation:10.0-b28'

  //rx
  implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
  implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
  implementation 'io.reactivex:rxkotlin:2.0.0-RC1'

  //timber
  implementation 'com.jakewharton.timber:timber:4.7.0'

  //leakcanary
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}


apply plugin: 'com.google.gms.google-services'
